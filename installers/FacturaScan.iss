; =========================
; FacturaScan - Inno Setup
; =========================
; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "FacturaScan"
#define MyAppVersion "1.9.4"
#define MyAppVersionFull "1.9.4.0"
#define MyAppPublisher "Departamento TI"
#define MyAppExeName "FacturaScan.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{75FCFDE0-F147-4AAD-86B0-9EA71650ED3A}}

AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppPublisher={#MyAppPublisher}

; --- Instalación en C:\
DefaultDirName=C:\{#MyAppName}
UsePreviousAppDir=yes
UninstallDisplayIcon={app}\{#MyAppExeName}

; Arquitectura
ArchitecturesAllowed=x64
ArchitecturesInstallIn64BitMode=x64

; UI
DisableProgramGroupPage=yes
WizardStyle=modern

; Salida del instalador
; Uncomment the following line to run in non administrative install mode (install for current user only).
;PrivilegesRequired=lowest
OutputDir=C:\Users\TEBA\Desktop
OutputBaseFilename=FacturaScan-{#MyAppVersion}-Setup

; Icono
SetupIconFile=C:\ProyectoFacturaScan\FacturaScan\src\facturascan\assets\FacturaScanInstaller.ico

; Compresión y logging
SolidCompression=yes
SetupLogging=yes

; Cierre automático y silencioso de FacturaScan
CloseApplications=force
CloseApplicationsFilter=FacturaScan.exe
RestartApplications=yes
AlwaysRestart=no

; ===== Metadata de versión para que no aparezca 0.0.0.0 =====
VersionInfoCompany={#MyAppPublisher}
VersionInfoDescription="{#MyAppName} Setup"
VersionInfoProductName="{#MyAppName}"
VersionInfoProductVersion={#MyAppVersion}
VersionInfoVersion={#MyAppVersionFull}

[Languages]
Name: "spanish"; MessagesFile: "compiler:Languages\Spanish.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; \
  GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
; Ejecutable principal explícito
Source: "C:\ProyectoFacturaScan\FacturaScan\dist\app.dist\{#MyAppExeName}"; \
    DestDir: "{app}"; Flags: ignoreversion
; Resto de dependencias generadas por Nuitka
Source: "C:\ProyectoFacturaScan\FacturaScan\dist\app.dist\*"; \
    DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs

[Icons]
; Menú Inicio
Name: "{autoprograms}\{#MyAppName}"; \
    Filename: "{app}\{#MyAppExeName}"; \
    AppUserModelID: "FacturaScan.App"

; Escritorio
Name: "{autodesktop}\{#MyAppName}"; \
    Filename: "{app}\{#MyAppExeName}"; \
    Tasks: desktopicon; \
    AppUserModelID: "FacturaScan.App"

[Run]
; ▶ Relanza SIEMPRE (también en /VERYSILENT): sin postinstall
Filename: "{app}\{#MyAppExeName}"; Flags: nowait

; Si prefieres solo en modo normal: usa esto en su lugar
; Filename: "{app}\{#MyAppExeName}"; Flags: nowait postinstall skipifsilent

[UninstallDelete]
; Limpia solo la carpeta de logs (todo su contenido)
Type: filesandordirs; Name: "{app}\logs"

[Code]
procedure KillIfRunning(const ExeName: string);
var
  ResultCode: Integer;
begin
  if FileExists(ExpandConstant('{sys}\taskkill.exe')) then
    Exec(ExpandConstant('{sys}\taskkill.exe'),
         '/IM "' + ExeName + '" /F',
         '', SW_HIDE, ewWaitUntilTerminated, ResultCode);
end;

procedure InitializeWizard();
begin
  KillIfRunning('FacturaScan.exe');
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
  if CurStep = ssInstall then
    KillIfRunning('FacturaScan.exe');
end;
